&НаСервере
Функция ВыгрузитьДокумент(ТекДокумент)
	ТипПриемНаРаботу = ФабрикаXDTO.Тип("http://www.sample-package.org", "ПриемНаРаботу");
	ОбъектПриемНаРаботу = ФабрикаXDTO.Создать(ТипПриемНаРаботу);	
	
	ТипДолжность = ФабрикаXDTO.Тип("http://www.sample-package.org", "Должности");
	ТипСотрудник = ФабрикаXDTO.Тип("http://www.sample-package.org", "Сотрудники");
	ТипТЧСотрудники = ФабрикаXDTO.Тип("http://www.sample-package.org", "ПриемНаРаботу.Сотрудники");
	ТипТЧСотрудникиСтрока = ФабрикаXDTO.Тип("http://www.sample-package.org", "ПриемНаРаботу.Сотрудники.Строка");
	ТипЗначенияПол = ФабрикаXDTO.Тип("http://www.sample-package.org", "Пол");
	
	ОбъектДолжность = ФабрикаXDTO.Создать(ТипДолжность);
	ОбъектДолжность.Наименование = ТекДокумент.ДолжностьСотрудника.Наименование;
	
	ОбъектТЧСотрудники = ФабрикаXDTO.Создать(ТипТЧСотрудники);
	Для Каждого СтрокаТЧ ИЗ ТекДокумент.Сотрудники Цикл
		ОбъектСотрудник	= ФабрикаXDTO.Создать(ТипСотрудник);
		ОбъектСотрудник.ФИО = СтрокаТЧ.Сотрудник.Наименование;
		ОбъектСотрудник.ДатаРождения = СтрокаТЧ.Сотрудник.ДатаРождения;
		Если СтрокаТЧ.Сотрудник.Пол = Перечисления.Пол.ПолЖенский Тогда
			ОбъектСотрудник.Пол = ФабрикаXDTO.Создать(ТипЗначенияПол, "ПолЖенский");
		ИначеЕсли СтрокаТЧ.Сотрудник.Пол = Перечисления.Пол.ПолМужской Тогда
			ОбъектСотрудник.Пол = ФабрикаXDTO.Создать(ТипЗначенияПол, "ПолМужской");
		КонецЕсли;
		
		ОбъектСтрокаТЧСотрудники = ФабрикаXDTO.Создать(ТипТЧСотрудникиСтрока);
		ОбъектСтрокаТЧСотрудники.Сотрудник = ОбъектСотрудник;
		ОбъектСтрокаТЧСотрудники.Оклад  = СтрокаТЧ.Оклад;	
		
		ОбъектТЧСотрудники.Строка.Добавить(ОбъектСтрокаТЧСотрудники);
	КонецЦикла;
	
	ОбъектПриемНаРаботу.Дата = ТекДокумент.Дата;
	ОбъектПриемНаРаботу.Номер = ТекДокумент.Номер;
	ОбъектПриемНаРаботу.Должность = ОбъектДолжность;
	ОбъектПриемНаРаботу.Сотрудники = ОбъектТЧСотрудники;
	
	Возврат ОбъектПриемНаРаботу; 
КонецФункции

&НаКлиенте
Процедура ИмяФайлаОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = "Файл данных: ";
	Диалог.Фильтр = "Файл XML (*.xml)|*.xml";
	Если Диалог.Выбрать() Тогда
		ИмяФайлаОбмена = Диалог.ПолноеИмяФайла;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДокументыНаСервере()
	Если ИмяФайлаОбмена = "" Тогда
		Возврат;	
	КонецЕсли;
	
	ФайлОбмена = Новый ЗаписьXML;
	ФайлОбмена.ОткрытьФайл(ИмяФайлаОбмена);
	
	ФайлОбмена.ЗаписатьОбъявлениеXML();
	ФайлОбмена.ЗаписатьНачалоЭлемента("Message");
	
	Выборка = Документы.ПриемНаРаботу.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектXDTO = ВыгрузитьДокумент(Выборка);
		
		ФабрикаXDTO.ЗаписатьXML(ФайлОбмена, ОбъектXDTO);
	КонецЦикла;
	
	ФайлОбмена.ЗаписатьКонецЭлемента();
	ФайлОбмена.Закрыть();	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументы(Команда)
	ВыгрузитьДокументыНаСервере();
КонецПроцедуры
